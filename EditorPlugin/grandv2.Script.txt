#RequireContext CMapEditorPlugin

#Include "TextLib" as TextLib
#Include "MathLib" as MathLib
#Include "TimeLib" as TimeLib
#Include "EditorPlugins/Grand2Lib/Ui.Script.txt" as Grand2UI
#Include "EditorPlugins/Grand2Lib/Connection.Script.txt" as Grand2

#Const	Version	"0.1"
#Const	ScriptName	"Grand2.Script.txt"

// externals
#Struct Grand2::sLobbyInfo as sLobbyInfo
#Struct Grand2::sServerInfo as sServerInfo
#Struct Grand2::ServerMessage as ServerMessage
#Struct Grand2::JsonBlock as JsonBlock

#Const C_CanyonRoadBlocks [
	"RoadMain"
]
#Const C_StadiumRoadBlocks [
	"StadiumRoadMain",
	"StadiumPlatformRoad",
	"StadiumRoadDirt",
	"StadiumRoadDirtHigh",
	"StadiumTubeRoad",
	"StadiumTubeRoadDown",
	"StadiumTubeRoadUp",
	"StadiumTubeRoadCross",
	"StadiumControlRoadGlass",
	"StadiumControlRoadPub",
	"StadiumControlRoadCamera",
	"StadiumTubeRoadLightSystem",
	"StadiumTubeRoadSoundSystem",
	"StadiumInflatableSupport"
]
#Const C_StormRoadBlocks [
	"RoadDirt"
]
#Const C_ValleyRoadBlocks [
	"Road2Way",
	"Road4Way",
	"RoadDirt"
]

// in milliseconds
#Const MinUpdateRate 5000

// Globals
declare Integer LastEdit;
declare	Boolean IsExternalEdit;
declare Boolean IsSyncingMap;
declare Text[] CurrentCollectionRoadBlocks;
declare Int3 SelectionStartCoord;
declare Int3 SelectionEndCoord;
declare Int3[Text] CustomCursors;

Void AddEventLine(Text _Line) {
	declare Text	Page_EventList			for ManialinkPage = "";
	Page_EventList ^= "\n" ^ _Line;
}

Void AddChatLine(Text _Line) {
	declare Text Page_ChatList for ManialinkPage = "";
	declare Text Page_ChatPopup for ManialinkPage = "";
	Page_ChatList ^= "\n" ^ _Line;
	Page_ChatPopup = _Line;

}


Integer getBlockDirection(CMapEditorPlugin::CardinalDirections direction) {
	switch (direction) {
			case CMapEditorPlugin::CardinalDirections::North:  {return 0;}
			case CMapEditorPlugin::CardinalDirections::East: {return 1;}
			case CMapEditorPlugin::CardinalDirections::South:{ return 2;}
			case CMapEditorPlugin::CardinalDirections::West:{ return 3;}
	}
	return 0;
}

CMapEditorPlugin::CardinalDirections convertBlockDirection(Integer direction) {
	switch (direction) {
			case 0: return CMapEditorPlugin::CardinalDirections::North;
			case 1: return CMapEditorPlugin::CardinalDirections::East;
			case 2: return CMapEditorPlugin::CardinalDirections::South;
			case 3: return CMapEditorPlugin::CardinalDirections::West;
	}
	return CMapEditorPlugin::CardinalDirections::North;
}
Void SyncMap(JsonBlock[] Blocks, Boolean doLog) {
			IsSyncingMap = True;
			foreach (eBlock in Blocks) {
			IsExternalEdit = True;
			LastEdit = Now;
				declare BlockModel = GetBlockModelFromName(eBlock.ModelName);
				switch (eBlock.Action) {
					case "Place": {
						declare temp = PlaceBlock(BlockModel, eBlock.CoordStart, convertBlockDirection(eBlock.Direction));
						if (doLog) {
						AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ eBlock.Sender ^ " $o+$o " ^ eBlock.ModelName ^ " at " ^ eBlock.CoordStart);
						}
					}
					case "Remove": {
						declare temp = RemoveBlock(eBlock.CoordStart);
					}
					case "PlaceRoad": {
						declare temp = PlaceRoadBlocks(BlockModel, eBlock.CoordStart, eBlock.CoordEnd);
						if (doLog) {
						AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ eBlock.Sender ^ " $o+$o " ^ eBlock.ModelName ^ " from " ^ eBlock.CoordStart ^ " to " ^ eBlock.CoordEnd);
						}
					}
					case "PlaceTerrain": {
						declare temp =PlaceTerrainBlocks(BlockModel, eBlock.CoordStart, eBlock.CoordEnd);
						if (doLog) {
						AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ eBlock.Sender ^ " $o+$o " ^ eBlock.ModelName ^ " from " ^ eBlock.CoordStart ^ " to " ^ eBlock.CoordEnd);
						}
					}
					case "EraseTerrain": {
					declare temp = RemoveTerrainBlocks(eBlock.CoordStart, eBlock.CoordEnd);
					if (doLog) {
						AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ eBlock.Sender ^ " $o-$o from " ^ eBlock.CoordStart ^ " to " ^ eBlock.CoordEnd);
						}
					}
				}
			}
			IsSyncingMap = False;
}



/* ==================================== 
 *  HTTP RESPONSE 
 * ===============================
 */
Void OnResponse(Text _response) {
	declare ServerMessage Data;
	declare val = Data.fromjson(_response);

	if (Data.Cursor.Owner != LocalUser.Name) {
		CustomCursors[Data.Cursor.Owner] = Data.Cursor.Position;
	}
	
	declare Integer i = 0;
	if (CustomSelectionCoords.count < CustomCursors.count) {
		CustomSelectionCoords.add(Data.Cursor.Position);
	}
	
	foreach (pos in CustomCursors) {
		CustomSelectionCoords[i] = pos;
		i+=1;
	}
		
	switch (Data.Action) {
		case "Chat": {
			AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ Data.ChatMessage);
		}
		case "ModifyMap": {
			SyncMap(Data.Blocks, True);
		}
	}
}

***OnInit***
***
declare Boolean	Page_LaunchPlugin for ManialinkPage = True;
declare Boolean	Page_Events for ManialinkPage;
declare Boolean Page_StopScript for ManialinkPage = False;

Grand2::setLastUpdate();
LayersDefaultManialinkVersion = 3;
ManialinkText = Grand2UI::getUi();
log("starting...");
log(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " > " ^ ScriptName ^ " V " ^ Version ^ " launched.");
IsExternalEdit = False;
IsSyncingMap = False;
***


***OnHttpRequest***
***

if(Request.Url == Grand2::GetPollUrl()) {
	if (Request.StatusCode == 200) {
		OnResponse(Request.Result);
	}
	log("waiting...");
	Grand2::Poll();
}

***


***OnMapActioniedBlockPlace***
***

if (IsExternalEdit == False) { 
	declare block = JsonBlock{Action = "Place", CoordStart = Cursor.Coord, ModelName = Cursor.BlockModel.Name, Direction = getBlockDirection(Cursor.Dir), Sender = LocalUser.Login};
	declare message = ServerMessage{Action="ModifyMap", Blocks=[block]};
	Grand2::SendMessage(message);
	
	AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ block.Sender ^ " $o+$o " ^ Cursor.BlockModel.Name ^ " at " ^ Cursor.Coord);
}
else {
IsExternalEdit = False; 
}

***

***OnMapActioniedBlockErase***
***

declare JsonBlock block = JsonBlock{Action = "Remove", 
																		CoordStart = Cursor.Coord,
																		Sender = LocalUser.Login
																		};
declare message = ServerMessage{Action="ModifyMap", Blocks=[block]};
Grand2::SendMessage(message);

AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ block.Sender ^ " $o-$o at " ^ Cursor.Coord);

***

***OnMapActioniedRoadPlace***
***
if (IsExternalEdit == False) {
	declare Action = "PlaceRoad"; 
		declare JsonBlock block = JsonBlock{
		Action = Action, 
		ModelName = Cursor.BlockModel.Name, 
		CoordStart = SelectionStartCoord,
		CoordEnd = SelectionEndCoord,
		Sender = LocalUser.Login
		};

	declare message = ServerMessage{Action="ModifyMap", Blocks=[block]};
	Grand2::SendMessage(message);
	AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ block.Sender ^ " $o+$o " ^ Cursor.BlockModel.Name ^ " from " ^ SelectionStartCoord ^ " to " ^ SelectionEndCoord);
}
else{
	IsExternalEdit = False; 
}

***

***OnMapActioniedTerrainPlace***
***
if (IsExternalEdit == False) {
	IsExternalEdit = False; 
	declare Action = "PlaceTerrain"; 
	
	declare JsonBlock block = JsonBlock{
	Action = Action,
	ModelName = Cursor.TerrainBlockModel.Name,
	CoordStart = SelectionStartCoord,
	CoordEnd = SelectionEndCoord,
	Sender = LocalUser.Login};

	declare message = ServerMessage{Action="ModifyMap", Blocks=[block]};
	Grand2::SendMessage(message);
	AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ block.Sender ^ " $o+$o " ^ Cursor.TerrainBlockModel.Name ^ " from " ^ SelectionStartCoord ^ " to " ^ SelectionEndCoord);
}




***

***OnMapActioniedTerrainErase***
***
if (IsExternalEdit) {
	declare Action = "EraseTerrain"; 
	
	declare JsonBlock block = JsonBlock
	{
	Action = Action, 
	ModelName = Cursor.TerrainBlockModel.Name, 
	CoordStart = SelectionStartCoord,
	CoordEnd = SelectionEndCoord,
	Sender = LocalUser.Login
	};

	declare message = ServerMessage{Action="ModifyMap", Blocks=[block]};
	Grand2::SendMessage(message);
	AddEventLine(TimeLib::FormatDate(TimeLib::GetCurrent(), TimeLib::EDateFormats::Time) ^ " $o|$o " ^ block.Sender ^ " $o-$o from " ^ SelectionStartCoord ^ " to " ^ SelectionEndCoord);
}
else {
	IsExternalEdit = False; 
}

***


Void dummy() {
}

/* --------------------- Main Script ------------------------ */

main() {

+++ OnInit +++
	switch (Map.MapInfo.CollectionName) {
				case "Canyon": CurrentCollectionRoadBlocks = C_CanyonRoadBlocks;
				case "Stadium": CurrentCollectionRoadBlocks = C_StadiumRoadBlocks;
				case "Storm": CurrentCollectionRoadBlocks = C_StormRoadBlocks;
				case "Valley": CurrentCollectionRoadBlocks = C_ValleyRoadBlocks;
	}
			
  while(True) {
    yield;
	sleep(5000);
	AddChatLine("Test");
	if (Page_StopScript) break;

 	foreach(HttpEvent in Http.PendingEvents) {
		declare Request = HttpEvent.Request;
		
		+++ OnHttpRequest +++
		
		Http.Destroy(Request);
	} // httpEvents

	  foreach (Event in PendingEvents) {			
			switch (Event.Type) {
				case CMapEditorPluginEvent::Type::LayerCustomEvent: {
						if(Event.CustomEventType == "start") {	 
							declare action = Event.CustomEventData[0];
							if (action == "1") {
							
							} else {
							
							}
						}
						if(Event.CustomEventType == "connectHost") {
							declare serverHost = Event.CustomEventData[0];
							declare	serverPassword = Event.CustomEventData[1];
							declare	sLobbyInfo info = Grand2::ConnectServer(serverHost, serverPassword);
							if (info.StatusCode == 200) {
							// @todo display here instance chooser layer
							
							declare ServerMessage mapMessage = Grand2::JoinInstance("0");
									if (mapMessage.Action == "SyncMap") {
											SyncMap(mapMessage.Blocks, False);
											Grand2::Poll();
									}
							}							
							continue;					
						}
						
				}
				case CMapEditorPluginEvent::Type::MapModified: {
					if(EditMode == ::EditMode::Unknown) {
						log("Place mode unknow");		
					}
					else if(EditMode == ::EditMode::Erase) {
						if(PlaceMode == ::PlaceMode::Block){
							+++ OnMapActioniedBlockErase +++
						}
					}
					else if (EditMode == ::EditMode::Place) {
						if(Cursor.BlockModel != Null) {
							if(PlaceMode == ::PlaceMode::Block) {
								if(!Cursor.BlockModel.IsRoad && !Cursor.BlockModel.IsTerrain && !CurrentCollectionRoadBlocks.exists(Cursor.BlockModel.Name)){
									+++ OnMapActioniedBlockPlace +++
								}
							}
						}
					}
					
				}
				case CMapEditorPluginEvent::Type::CursorSelectionBegin: {
					SelectionStartCoord = Cursor.Coord;
				}
				case CMapEditorPluginEvent::Type::CursorSelectionEnd: {
					SelectionEndCoord = Cursor.Coord;
					switch (PlaceMode) {
						case ::PlaceMode::Block: {
							if (EditMode == ::EditMode::Place) {
								if (Cursor.BlockModel != Null && CurrentCollectionRoadBlocks.exists(Cursor.BlockModel.Name)){
									if (SelectionStartCoord == SelectionEndCoord){
										+++ OnMapActioniedBlockPlace +++
									}
									else {
										+++ OnMapActioniedRoadPlace +++
									}
								}
							}
						}
						
						case ::PlaceMode::Terraform: {
						
							if (EditMode == ::EditMode::Place && Cursor.TerrainBlockModel != Null){
								+++ OnMapActioniedTerrainPlace +++
							}else if (EditMode == ::EditMode::Erase){
								+++ OnMapActioniedTerrainErase +++
							}
						}
					}															
					SelectionStartCoord = <0, 0, 0>;
					SelectionEndCoord = <0, 0, 0>;
				}	
			}
			if (IsExternalEdit == True && IsSyncingMap == False){
					if((Now - LastEdit) > 10 ) {
						IsExternalEdit = False;
					}
			}			
		} // pending events
		
		// sync cursor every 5 seconds
		if (Grand2::isConnected()) {
			if (Now - Grand2::GetLastUpdate() > MinUpdateRate) {
					declare message = ServerMessage{Action="SyncCursor"};			
					Grand2::SendMessage(message);				
			}
		}
		
		+++ Loop +++
  }
}
