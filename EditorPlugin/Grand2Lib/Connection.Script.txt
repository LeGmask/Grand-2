/** @context CMlScriptIngame */
#Include "TextLib" as TextLib
#Include "MathLib" as MathLib
#Include "TimeLib" as TimeLib

#Struct sServerInfo {
	Text Host;
	Text Password;
	Text Token;
	Text InstanceId;
	Boolean IsConnected;
}

#Struct sUser {
	Text Login;
	Text Nickname;
	Boolean isAdmin;
}

#Struct sChannel {
	Text Id;
	Text Admin;
	sUser[] Users;
}

#Struct sLobbyInfo {
	Integer StatusCode;
	Text Token;	
	sChannel[] Sessions;		
}


// serverMessage structs
#Struct JsonBlock {
	Text 		Action;
	Text TimeStamp;
	Text		Sender;
	Text 		ModelName;
	Int3 		CoordStart;
	Int3 		CoordEnd;
	Integer 	Direction;
}

#Struct JsonCursor {
	Text Owner;
	Int3 Position;
	Vec3 Color;
}

#Struct ServerMessage {
	Text Action;
	Text ChatMessage;
	JsonBlock[] Blocks;
	JsonCursor Cursor;
}


declare sServerInfo Grand2_serverInfo;
declare Integer LastUpdate;

CHttpRequest HttpGet(Text _address) {
		return Http.CreateGet(_address, False);
}

CHttpRequest HttpPost(Text _address, Text[Text] _params) {
	declare helper = _params;
	if (Grand2_serverInfo.Token != "") {
		helper["token"] = Grand2_serverInfo.Token;
	}
	log(helper);
	declare content = "";	
	foreach (Key => Param in helper) {
		content ^= Key ^ "=" ^ Param ^"&";
	}
	
	declare len = TextLib::Length(content)-1;	
	return Http.CreatePost(_address, TextLib::SubString(content, 0,len), "authorization: "^TextLib::MLEncode(Grand2_serverInfo.Password)^"\nContent-Type: application/x-www-form-urlencoded");
	
}

Text HttpPostSync(Text _address, Text[Text] _params, Text _headers) {
	declare CHttpRequest req = HttpPost(_address, _params);
	
	wait(req.IsCompleted);
	declare output = "";
	if (req.StatusCode == 200) {
		output = req.Result;
		Http.Destroy(req);
		return output;
	} 
	
	return output;	
}

sLobbyInfo ConnectServer(Text _address, Text _password) {
	declare Text[Text] params = [
		"login" => LocalUser.Login,
		"nickname" => LocalUser.Name
	];
	Grand2_serverInfo = sServerInfo{Host=_address, Password=_password};
	
	declare CHttpRequest req = HttpPost(_address^"/editor/lobby", params);
	wait(req.IsCompleted);
	declare sLobbyInfo output = sLobbyInfo{StatusCode=req.StatusCode};
	
	if (req.StatusCode == 200) {
		declare sLobbyInfo output;	
		declare success = output.fromjson(req.Result);
		Grand2_serverInfo.Token = output.Token;
		output.StatusCode = req.StatusCode;
		Http.Destroy(req);	
		return output;
	}
	
	Http.Destroy(req);	
	return output;
}

Text GetPollUrl() {
	return Grand2_serverInfo.Host^"/editor/"^Grand2_serverInfo.InstanceId^"/listener";
}

Integer GetLastUpdate() {
	return LastUpdate;
}

Boolean isConnected() {
	return Grand2_serverInfo.IsConnected;
}

Void setLastUpdate() {
	LastUpdate = Now;
}

Void Poll() {
		declare req = HttpPost(GetPollUrl(), []);
}

Void SendMessage(ServerMessage message) {
	LastUpdate = Now;
	declare helperMessage = message;
	helperMessage.Cursor = JsonCursor{Owner=LocalUser.Name, Position=Cursor.Coord};
	foreach (key => value in helperMessage.Blocks) {
		helperMessage.Blocks[key].TimeStamp = TimeLib::GetCurrent();	
	}
	declare req = HttpPost(Grand2_serverInfo.Host^"/editor/"^Grand2_serverInfo.InstanceId^"/push", ["data" => helperMessage.tojson()]);
}

ServerMessage JoinInstance(Text _instanceId) {
	declare ServerMessage message = ServerMessage{Action = "JoinInstance"};
	message.Cursor = JsonCursor{Owner=LocalUser.Name, Position=Cursor.Coord };
	
	declare CHttpRequest req = HttpPost(Grand2_serverInfo.Host^"/editor/"^_instanceId^"/join", ["data" => message.tojson()]);
	wait(req.IsCompleted);
	declare ServerMessage outMessage;
	if (req.StatusCode == 200) {
		Grand2_serverInfo.InstanceId = _instanceId;
		Grand2_serverInfo.IsConnected = True;
		declare success = outMessage.fromjson(req.Result);
		Http.Destroy(req);
		return outMessage;				
	}
	Http.Destroy(req);	
	Grand2_serverInfo.InstanceId = "";
	return outMessage;
}



